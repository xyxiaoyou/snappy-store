/*
 * Copyright (c) 2017-2019 TIBCO Software Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you
 * may not use this file except in compliance with the License. You
 * may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
 * implied. See the License for the specific language governing
 * permissions and limitations under the License. See accompanying
 * LICENSE file.
 */

dependencies {
  compileOnly "org.apache.thrift:libthrift:${thriftVersion}"
  compile "net.java.dev.jna:jna:${jnaVersion}"
  compile "commons-io:commons-io:${commonsIoVersion}"

  compile project(subprojectBase + 'gemfire-shared')
  compile project(subprojectBase + 'gemfire-trove')
}

ext {
  isEnterprise = 'false'
}

// Creates the version properties file and writes it to the resources dir
task createVersionPropertiesFile(dependsOn: 'processResources') {
  def propertiesDir = file("${sourceSets.main.java.outputDir}/com/pivotal/gemfirexd/internal")
  outputs.file "${propertiesDir}/GemFireXDVersion.properties"
  inputs.file "${rootProject.projectDir}/build.gradle"
  if (rootProject.hasProperty('snappydata.enterprise')) {
    isEnterprise = 'true'
  }

  doLast {
    def props = [
      'Custom-Edition'    : CUSTOM_EDITION,
      'Product-Name'      : PRODUCT_NAME,
      'Product-Version'   : version,
      'Enterprise-Edition': isEnterprise,
      'Build-Id'          : buildIdPrefix + buildNumber,
      'Build-Date'        : buildDate,
      'Build-Platform'    : "${osName.getName()} ${osVersion} ${osArch}",
      'Build-Java-Version': jdkVersion,
      'Source-Date'       : sourceDate,
      'Source-Revision'   : commitId,
      'Source-Repository' : gitBranch,
      'Product-Stage'     : PRODUCT_RELEASE_STAGE,
      'GemFire-Version'   : GEMFIRE_VERSION
    ]

    writeProperties(propertiesDir, 'GemFireXDVersion.properties',
        "Properties that control what version ${PRODUCT_NAME} will think it is. Changing these values may cause ${PRODUCT_NAME} to no longer function.", props)
  }
}

compileJava.dependsOn createVersionPropertiesFile

// external javadocs
javadoc {
  include 'com/pivotal/gemfirexd/*.java'
}
javadoc.dependsOn.clear()

archivesBaseName = 'snappydata-store-shared'
